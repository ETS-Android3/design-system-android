apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

version = '3.1.0'
group = 'com.salesforce.ux'

android {
    compileSdkVersion 24

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 24
        versionCode 3
        versionName version

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:24.2.1'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                pom {
                    name = 'SalesforceDesignSystem'
                    url = 'https://github.com/salesforce-ux/design-system-android'

                    licenses {
                        license {
                            name = 'BSD 3-clause'
                            url = 'https://github.com/salesforce-ux/design-system-android/blob/master/LICENSE.txt'
                        }
                    }

                    developers {
                        developer {
                            name = 'Salesforce UX'
                        }
                    }

                    scm {
                        connection = 'https://github.com/salesforce-ux/design-system-android.git'
                        developerConnection = 'https://github.com/salesforce-ux/design-system-android.git'
                        url = 'https://github.com/salesforce-ux/design-system-android'
                    }
                }
            }
        }
    }
}

